apply plugin: 'com.android.library'
apply plugin: 'checkstyle'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            testLogging {
                exceptionFormat = 'full'
            }
        }
    }

    lintOptions {
        // We have okio as a transitive dependency, which causes some lint errors due to
        // using Java 8 nio packages. Safely downgrading them to warning.
        // https://github.com/square/okio/issues/58
        warning 'InvalidPackage'
    }
}

dependencies {
    compile "com.android.support:design:25.1.0"
    compile "com.android.support:support-v4:25.1.0"
    compile "com.google.firebase:firebase-auth:10.0.1"
    compile "com.google.android.gms:play-services-auth:10.0.1"

    // The following libraries are needed to prevent incompatibilities with the facebook
    // library when updating com.android.support libraries:
    compile "com.android.support:customtabs:25.1.0"
    compile "com.android.support:cardview-v7:25.1.0"
    compile 'com.firebaseui:firebase-ui-auth:1.0.1'
    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-database:10.0.1'
    compile 'com.google.firebase:firebase-storage:10.0.1'
    compile 'com.google.firebase:firebase-auth:10.0.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.28'
    testCompile 'org.robolectric:robolectric:3.1.4'
    // See https://github.com/robolectric/robolectric/issues/1932#issuecomment-219796474
    testCompile 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
}

checkstyle {
    configFile file('../common/checkstyle/checkstyle.xml')
    configProperties = ['checkstyle.java.header': file('../common/checkstyle/java.header').getAbsolutePath()]
    toolVersion = "6.11.2"
}

task checkAllSource(type: Checkstyle) {
    source 'src/main/java'
    include '**/*.java'
    classpath = files(project.configurations.compile)
}

javadoc.exclude([
        'com/firebase/ui/auth/provider/**',
        'com/firebase/ui/auth/ui/ExtraConstants',
        'com/firebase/ui/auth/ui/email/field_validators/**',
        'com/firebase/ui/auth/util/**',
])

javadoc.include([
        'com/google/firebase/auth/**'
])
